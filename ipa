#!/usr/bin/env perl
use utf8;
binmode( STDOUT, ":utf8" );

use strict;
use warnings;

use Getopt::Long;
use Mojo::Base -strict;
use Mojo::UserAgent;

my $keep_slashes       = 0;
my $all_pronunciations = 0;

GetOptions(
    "slash" => \$keep_slashes,
    "all"   => \$all_pronunciations
);

@ARGV = qw/please specify phrase/ if not @ARGV;

my $ua = Mojo::UserAgent->new;

for (@ARGV) {
    my $orig = lc;
    my $page = $ua->get("http://en.wiktionary.org/wiki/$orig")->res->dom;

    my $spans = $page->find('.IPA');
    if ($all_pronunciations) {
        if ( $spans->size > 0 ) {
            print $spans->map(
                sub {
                    my $txt = $_->text;
                    $txt =~ s/\/(.*)\//$1/g if not $keep_slashes;
                    $txt;
                }
            )->join(" ");
            print " ";
        }
        else {
            print "$orig ";
        }
    }
    else {
        my $txt;
        if ( $spans->size > 0 ) {
            $txt = $spans->shuffle->first->text || $orig;
            $txt =~ s/\/(.*)\//$1/g if not $keep_slashes;
        }
        else {
            $txt = $orig;
        }
        print "$txt ";
    }
}

print "\n";
